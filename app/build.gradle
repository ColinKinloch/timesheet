apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def getGitSha = {
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}

ext.APP_NAME = 'Timesheet DX'

android {
  compileSdkVersion 31

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 31

    versionCode 14
    versionName '1.7'

    multiDexEnabled true
    // vectorDrawables.useSupportLibrary = true
    resValue "string", "app_name", APP_NAME
  }

  signingConfigs {
    release {
      storeFile file('../store.jks')
      storePassword System.getenv('STORE_PASSWORD')
      keyAlias System.getenv('KEY_ALIAS')
      keyPassword System.getenv('KEY_PASSWORD')
    }
  }

  buildTypes {
    release {
      shrinkResources true
      minifyEnabled true

      proguardFiles file('proguard-rules.pro')
      signingConfig signingConfigs.release
    }

    debug {
      //applicationIdSuffix '.debug'
    }
  }

  flavorDimensions "color"
  productFlavors {
    blue {}
    green {
      resValue "string", "app_name", APP_NAME + " Test"
      applicationIdSuffix ".test"
      versionNameSuffix "-" + getGitSha()

    }
  }

  buildFeatures {
    //viewBinding true
  }

  lintOptions {
    disable 'MissingTranslation'
    abortOnError false
  }

  compileOptions {
    //coreLibraryDesugaringEnabled true

    //sourceCompatibility JavaVersion.VERSION_1_8
    //targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  def xcore_version = "1.7.0"
  def xappcompat_version = "1.4.0"
  def xpreference_version = "1.1.1"
  def xfragment_version = "1.4.0"


  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.ext.kotlinVersion"
  
  implementation "androidx.core:core:$xcore_version"
  implementation "androidx.core:core-ktx:$xcore_version"

  implementation "androidx.appcompat:appcompat:$xappcompat_version"
  // For loading and tinting drawables on older versions of the platform
  implementation "androidx.appcompat:appcompat-resources:$xappcompat_version"


  // Java language implementation
  implementation "androidx.fragment:fragment:$xfragment_version"
  // Kotlin
  implementation "androidx.fragment:fragment-ktx:$xfragment_version"

  implementation "androidx.preference:preference:$xpreference_version"
  implementation "androidx.preference:preference-ktx:$xpreference_version"
}
